# Give me the artists who collaborated with "Ibaya" and the ones similar to each of them
PREFIX rs: <http://www.researchspace.org/ontology/>
PREFIX wdt: <http://www.wikidata.org/prop/direct/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd: <http://www.wikidata.org/entity/>
PREFIX rso: <http://www.researchspace.org/ontology/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX word2vec: <http://www.metaphacts.com/ontology/word2vec/>
SELECT * WHERE {
    SERVICE <http://www.metaphacts.com/ontologies/platform/repository/federation#wikidata-text>  {
                  ?wikidataIRI <http://www.metaphacts.com/ontology/hasWikidataSearchToken> "ibaya" .
        }
    SERVICE <https://public.researchspace.org/sparql> {
      ?bmIRI skos:exactMatch ?wikidataIRI .
      ?bmIRI rs:Actor_is_creator_of_Thing ?artifact .
      ?collabBMIRI rs:Actor_is_creator_of_Thing ?artifact .
      ?collabBMIRI skos:exactMatch ?collabWikidataIRI .
      FILTER (?bmIRI != ?collabBMIRI)
    }
    ?collabWikidataIRI rdfs:label ?label .
    SERVICE <http://www.metaphacts.com/ontologies/platform/repository/federation#word2vec> {
       ?collabWikidataIRI word2vec:hasSimilar ?artist .
    }
  }
